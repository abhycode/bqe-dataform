config {
  type: "table",
  schema: dataform.projectConfig.defaultSchema,
  dependencies: ["jobs_timeline"],
  bigquery: {
    partitionBy: "DATE(usage_time)",
    clusterBy: ["project_id"]
  },
  assertions: {
    uniqueKey: ["bq_region", "project_id", "usage_time"]
  },
  tags: ["TRANSFORM_DATA"]
}


SELECT
    bq_region
  , project_id
  , reservation_id
  , pricing_model
  , usage_time
  , SUM(total_usage_slot_ms) AS total_usage_slot_ms
  , SUM(total_bytes_processed) AS total_bytes_processed
  , ROUND(CEIL(MAX(total_usage_slot_ms) / 100000) * 100) AS max_slot_per_minute
  , ROUND(CEIL(IFNULL(MAX(IF(slot_rank = 2, total_usage_slot_ms, NULL))
                    , MAX(total_usage_slot_ms)) / 100000) * 100) AS second_largest_slot_per_minute
FROM (SELECT
          bq_region
        , project_id
        , reservation_id
        , pricing_model
        , usage_time
        , total_usage_slot_ms
        , total_bytes_processed
        , RANK() OVER (PARTITION BY bq_region
                                  , project_id
                                  , reservation_id
                                  , pricing_model
                                  , usage_time
                       ORDER BY total_usage_slot_ms DESC) AS slot_rank
      FROM (SELECT
                bq_region
              , project_id
              , reservation_id
              , pricing_model
              , TIMESTAMP_TRUNC(period_start, SECOND ) AS usage_time_sec
              , TIMESTAMP_TRUNC(period_start, MINUTE) AS usage_time
              , IFNULL(SUM(period_slot_ms), 0) AS total_usage_slot_ms
              , IFNULL(SUM(total_bytes_processed), 0) AS total_bytes_processed
            FROM ${ ref({ schema: dataform.defaultSchema, name: "jobs_timeline" }) }
            GROUP BY bq_region
                   , project_id
                   , reservation_id
                   , pricing_model
                   , usage_time_sec
                   , usage_time) AS dt1
      WHERE total_usage_slot_ms > 0
         OR total_bytes_processed > 0) AS dt2
GROUP BY bq_region
       , project_id
       , reservation_id
       , pricing_model
       , usage_time
