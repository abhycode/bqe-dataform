config {
  type: "table",
  schema: dataform.projectConfig.defaultSchema,
  dependencies: ["jobs_timeline"],
  bigquery: {
    partitionBy: "DATE(usage_time)",
    clusterBy: ["project_id"]
  },
  assertions: {
    uniqueKey: ["bq_region", "project_id", "usage_time"]
  },
  tags: ["TRANSFORM_DATA"]
}


SELECT
    bq_region
  , project_id
  , reservation_id
  , pricing_model
  , usage_time
  , SUM(usage_slot_sec) AS total_usage_slot_ms
  , SUM(total_bytes_processed) AS total_bytes_processed
  , ROUND(CEIL(MAX(usage_slot_sec) / 100000) * 100) AS max_slot_per_minute
  , ROUND(CEIL(IFNULL(MAX(IF(slot_rank = 2, usage_slot_sec, NULL))
                    , MAX(usage_slot_sec)) / 100000) * 100) AS second_largest_slot_per_minute
  , ARRAY_AGG(usage_slot_sec ORDER BY usage_time_sec ASC) AS usage_slot_sec
FROM (SELECT
          bq_region
        , project_id
        , reservation_id
        , pricing_model
        , usage_time_sec
        , usage_time
        , usage_slot_sec
        , total_bytes_processed
        , ROW_NUMBER() OVER (PARTITION BY bq_region
                                        , project_id
                                        , reservation_id
                                        , pricing_model
                                        , usage_time
                             ORDER BY usage_slot_sec DESC) AS slot_rank
      FROM (SELECT
                bq_region
              , project_id
              , reservation_id
              , pricing_model
              , TIMESTAMP_TRUNC(period_start, SECOND) AS usage_time_sec
              , TIMESTAMP_TRUNC(period_start, MINUTE) AS usage_time
              , IFNULL(SUM(period_slot_ms), 0) AS usage_slot_sec
              , IFNULL(SUM(total_bytes_processed), 0) AS total_bytes_processed
            FROM ${ ref({ schema: dataform.defaultSchema, name: "jobs_timeline" }) }
            GROUP BY bq_region
                   , project_id
                   , reservation_id
                   , pricing_model
                   , usage_time_sec
                   , usage_time) AS dt1) AS dt2
GROUP BY bq_region
       , project_id
       , reservation_id
       , pricing_model
       , usage_time
