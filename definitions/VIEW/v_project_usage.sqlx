config {
  type: "view",
  schema: dataform.projectConfig.vars.BASE_DATA,
  dependencies: ["project_usage"],
  tags: ["VIEW_DATA"]
}


SELECT
    pu.bq_region
  , pu.project_id
  , commitment_option
  , TIMESTAMP_DIFF(TIMESTAMP_ADD(TIMESTAMP("${ dataform.projectConfig.vars.VIEW_END_DATE }"), INTERVAL 1 DAY)
                 , TIMESTAMP("${ dataform.projectConfig.vars.VIEW_START_DATE }"), MINUTE) AS n_sample_minutes
  , SUM(IF(pu.next_largest_slot_per_min > commitment_option, pu.next_largest_slot_per_min - commitment_option, 0)) AS autoscale_amount
FROM ${ ref({ schema: dataform.projectConfig.vars.BASE_DATA, name: "project_usage" }) } AS pu
  INNER JOIN (SELECT
                  bq_region
                , project_id
                , GENERATE_ARRAY(0, MAX(max_slot_per_min), 100) AS commitment_options
              FROM ${ ref({ schema: dataform.projectConfig.vars.BASE_DATA, name: "project_usage" }) }
              WHERE usage_time_min >= "${ dataform.projectConfig.vars.VIEW_START_DATE }"
                AND usage_time_min <  TIMESTAMP_ADD(TIMESTAMP("${ dataform.projectConfig.vars.VIEW_END_DATE }"), INTERVAL 1 DAY)
              GROUP BY bq_region
                     , project_id) AS co
    ON  pu.bq_region = co.bq_region
    AND pu.project_id = co.project_id
  CROSS JOIN UNNEST(co.commitment_options) AS commitment_option
WHERE pu.usage_time_min >= "${ dataform.projectConfig.vars.VIEW_START_DATE }"
  AND pu.usage_time_min <  TIMESTAMP_ADD(TIMESTAMP("${ dataform.projectConfig.vars.VIEW_END_DATE }"), INTERVAL 1 DAY)
GROUP BY pu.bq_region
       , pu.project_id
       , commitment_option
